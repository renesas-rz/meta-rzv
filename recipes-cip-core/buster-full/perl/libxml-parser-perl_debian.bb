# base recipe: meta/recipes-devtools/perl/libxml-parser-perl_2.44.bb
# base branch: warrior
# base commit: 720f7554a8cc3ca15321e010b9f1348cd7bfe23c

SUMMARY = "XML::Parser - A perl module for parsing XML documents"
HOMEPAGE = "https://libexpat.github.io/"
SECTION = "libs"
LICENSE = "Artistic-1.0 | GPL-1.0+"
LIC_FILES_CHKSUM = "file://README;beginline=2;endline=6;md5=c8767d7516229f07b26e42d1cf8b51f1"

inherit debian-package-buster
require libxml-parser-perl.inc
DEBIAN_UNPACK_DIR = "${WORKDIR}/XML-Parser-2.44"

DEPENDS += "expat"

EXTRA_CPANFLAGS = "EXPATLIBPATH=${STAGING_LIBDIR} EXPATINCPATH=${STAGING_INCDIR} CC='${CC}' LD='${CCLD}' FULL_AR='${AR}'"

inherit cpan ptest-perl perl-version-buster

# fix up sub MakeMaker project as arguments don't get propagated though
# see https://rt.cpan.org/Public/Bug/Display.html?id=28632
do_configure_append_class-target() {
	sed -E \
	    -e 's:-L${STAGING_LIBDIR}::g' -e 's:-I${STAGING_INCDIR}::g' \
	    -i Makefile Expat/Makefile
}

do_configure_append() {
	sed -e 's:--sysroot=.*\(\s\|$\):--sysroot=${STAGING_DIR_TARGET} :g' \
	    -i Makefile Expat/Makefile
	sed 's:^FULL_AR = .*:FULL_AR = ${AR}:g' -i Expat/Makefile
	# make sure these two do not build in parallel
	sed 's!^$(INST_DYNAMIC):!$(INST_DYNAMIC): $(BOOTSTRAP)!' -i Expat/Makefile
}

do_compile() {
	export LIBC="$(find ${STAGING_DIR_TARGET}/${base_libdir}/ -name 'libc-*.so')"
	cpan_do_compile
}

do_compile_class-native() {
	cpan_do_compile
}

do_install_ptest() {
	sed -i -e "s:/usr/local/bin/perl:/usr/bin/perl:g" ${B}/samples/xmlstats
	sed -i -e "s:/usr/local/bin/perl:/usr/bin/perl:g" ${B}/samples/xmlfilter
	sed -i -e "s:/usr/local/bin/perl:/usr/bin/perl:g" ${B}/samples/xmlcomments
	sed -i -e "s:/usr/local/bin/perl:/usr/bin/perl:g" ${B}/samples/canonical
	cp -r ${B}/samples ${D}${PTEST_PATH}
	chown -R root:root ${D}${PTEST_PATH}/samples
}

RDEPENDS_${PN}-ptest += "perl-module-filehandle perl-module-if perl-module-test perl-module-test-more"

BBCLASSEXTEND="native nativesdk"

# fix different dir paths due to old classes in poky, mostly change perl -> perl5
export PERL_INC = "${STAGING_LIBDIR}${PERL_OWN_DIR}/perl5/${@get_perl_version(d)}/${@get_perl_arch(d)}/CORE"
export PERL_LIB = "${STAGING_LIBDIR}${PERL_OWN_DIR}/perl5/${@get_perl_version(d)}"
export PERL_ARCHLIB = "${STAGING_LIBDIR}${PERL_OWN_DIR}/perl5/${@get_perl_version(d)}/${@get_perl_arch(d)}"
export PERLHOSTLIB = "${STAGING_LIBDIR_NATIVE}/perl5/${@get_perl_version(d)}/"
export PERLHOSTARCHLIB = "${STAGING_LIBDIR_NATIVE}/perl5/${@get_perl_version(d)}/${@get_perl_hostarch(d)}/"

FILES_${PN} += "${libdir}/perl5 ${datadir}/perl5"

# Overwrit config function to change dir for config.sh
cpan_do_configure () {
        export PERL5LIB="${PERL_ARCHLIB}"
        yes '' | perl ${EXTRA_PERLFLAGS} Makefile.PL INSTALLDIRS=vendor ${EXTRA_CPANFLAGS}

        # Makefile.PLs can exit with success without generating a
        # Makefile, e.g. in cases of missing configure time
        # dependencies. This is considered a best practice by
        # cpantesters.org. See:
        #  * http://wiki.cpantesters.org/wiki/CPANAuthorNotes
        #  * http://www.nntp.perl.org/group/perl.qa/2008/08/msg11236.html
        [ -e Makefile ] || bbfatal "No Makefile was generated by Makefile.PL"

        if [ "${BUILD_SYS}" != "${HOST_SYS}" ]; then
                . ${STAGING_LIBDIR}${PERL_OWN_DIR}/perl5/config.sh
                # Use find since there can be a Makefile generated for each Makefile.PL
                for f in `find -name Makefile.PL`; do
                        f2=`echo $f | sed -e 's/.PL//'`
                        test -f $f2 || continue
                        sed -i -e "s:\(PERL_ARCHLIB = \).*:\1${PERL_ARCHLIB}:" \
                                -e 's/perl.real/perl/' \
                                -e "s|^\(CCFLAGS =.*\)|\1 ${CFLAGS}|" \
                                $f2
                done
        fi
}

